plugins {
    id "java-library"
    id "maven-publish"
    id "org.jreleaser" version "1.22.0"
    id "com.diffplug.spotless" version "6.25.0"
}

project.version = '1.4.0'
group = "org.komamitsu"

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestCompileOnly.extendsFrom testCompileOnly
}

dependencies {
    implementation "org.slf4j:slf4j-api:2.0.11"
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation platform("org.springframework.boot:spring-boot-dependencies:3.3.5")
    api "org.springframework.data:spring-data-jdbc"
    implementation "org.xerial:sqlite-jdbc:3.50.3.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.2"
    testImplementation "org.assertj:assertj-core:3.27.6"
    testImplementation "org.mockito:mockito-core:5.21.0"
    testImplementation "org.mockito:mockito-junit-jupiter:5.9.0"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    integrationTestImplementation "com.zaxxer:HikariCP:5.1.0"
}

tasks.withType(Test) {
    useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
    description = 'Runs the integration tests'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }  // ensures integration tests are run every time when called
}

compileIntegrationTestJava {
    options.compilerArgs << "-parameters"
}

def stagingDir = layout.buildDirectory.dir("staging-deploy")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'spring-data-sqlite'
            from components.java
            pom {
                name = 'Spring Data integration with SQLite'
                description = 'Spring Data integration with SQLite'
                url = 'https://github.com/komamitsu/spring-data-sqlite'
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = 'komamitsu'
                        name = 'Mitsunori Komatsu'
                        email = 'komamitsu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/komamitsu/spring-data-sqlite.git'
                    developerConnection = 'scm:git:https://github.com/komamitsu/spring-data-sqlite.git'
                    url = 'https://github.com/komamitsu/spring-data-sqlite'
                }
            }
        }
    }
    repositories {
        maven {
            name = "staging"
            url = uri(stagingDir)
        }
    }
}

jreleaser {
    signing {
        pgp {
            active = 'ALWAYS'
            armored = true
        }
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository(stagingDir.get().asFile.path)
                }
            }
        }
    }
}

spotless {
  java {
    importOrder()
    removeUnusedImports()
    googleJavaFormat()
    formatAnnotations()
  }
}
